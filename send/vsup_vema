#!/usr/bin/perl
use strict;
use warnings FATAL => 'all';
use DBI;
use Getopt::Long qw(:config no_ignore_case);
use Data::Dumper;
use ScriptLock;

my $username;
my $password;
my $tableName_ID = 'idmpamid';
my $tableName_SPJ = 'idmpamsp';

# define service
my $service_name = "vsup_vema";

# GEN folder location
my $facility_name = $ARGV[0];
chomp($facility_name);
my $service_files_base_dir="../gen/spool";
my $service_files_dir="$service_files_base_dir/$facility_name/$service_name";
my $service_file = "$service_files_dir/$service_name.csv";

# propagation destination
my $destination = $ARGV[1];
chomp($destination);

# create service lock
my $lock = ScriptLock->new($facility_name . "_" . $service_name . "_" . $destination);
($lock->lock() == 1) or die "Unable to get lock, service propagation was already running.";

# parse destination
my ($db_machine,$db_port,$db_name) = split(/:/, $destination);

# load authz
my $configPath = "/etc/perun/services/$service_name/$db_name";
open FILE, $configPath or die "Could not open config file $configPath: $!";
while(my $line = <FILE>) {
	if($line =~ /^username: .*/) {
		$username = ($line =~ m/^username: (.*)$/)[0];
	} elsif($line =~ /^password: .*/) {
		$password = ($line =~ m/^password: (.*)$/)[0];
	}
}

if(!defined($password) || !defined($username) || !defined($tableName_ID) || !defined($tableName_SPJ)) {
	print "Can't get config data from config file.\n";
	exit 14;
}

#Main Structure
my $dataByOsbId = {};

open FILE, $service_file or die "Could not open $service_file: $!";
while(my $line = <FILE>) {
	my @parts = split /\t/, $line;
	$dataByOsbId->{$parts[0]}->{'UCO'} = $parts[1];
	$dataByOsbId->{$parts[0]}->{'LOGIN'} = $parts[2];
	$dataByOsbId->{$parts[0]}->{'EMAIL'} = $parts[3];
}
close FILE;

my $dbh = DBI->connect("dbi:Oracle://$db_machine:$db_port/$db_name", $username, $password,{ RaiseError=>1, AutoCommit=>0, LongReadLen=>65536, ora_charset => 'AL32UTF8'}) or die "Connect to database $db_name Error!\n";

my $DEBUG=0;
#statistic and information variables
my $foundAndSkipped = 0;
my $foundAndUpdated = 0;
my $inserted = 0;
my $foundAndSkipped_SPJ = 0;
my $foundAndUpdated_SPJ = 0;
my $inserted_SPJ = 0;

#update and insert new
foreach my $osbId (sort keys $dataByOsbId) {

	my $UCO = $dataByOsbId->{$osbId}->{'UCO'};
	my $LOGIN = $dataByOsbId->{$osbId}->{'LOGIN'};
	my $EMAIL = $dataByOsbId->{$osbId}->{'EMAIL'};

	# PROCESS ID TABLE - LOGIN

	my $personExists = $dbh->prepare(qq{select 1 from $tableName_ID where OSB_ID=?});
	$personExists->execute($osbId);

	if($personExists->fetch) {
		if($DEBUG == 1) { print "FIND: $osbId\n"; }
		# for now we never update existing entry
		if($DEBUG == 1) { print "SKIP RECORD: $osbId\n"; }
		$foundAndSkipped++;

	} else {

		if($DEBUG == 1) { print "INSERT NEW RECORD: $osbId\n"; }
		$inserted++;

		# FIXME - column ZAPL - start of validity will be set to now()
		# FIXME - column KOPL - we will probably set end of validity, if user is missing from export or we know last date of relation on VÅ UP

		# we will do insert
		my $insertPerson = $dbh->prepare(qq{INSERT INTO $tableName_ID (OSB_ID, UCO, ZAPL, TYP_PROP, KOD, ZMENENO) VALUES (?,?,sysdate,?,?,sysdate)});
		$insertPerson->execute($osbId, $UCO, 'vmauth', $LOGIN);

	}

	# TODO - process KOPL or remove entries ?

	# PROCESS SPJ TABLE - UCO (we never update or remove UCO)

	my $ucoExists = $dbh->prepare(qq{select 1 from $tableName_SPJ where OSB_ID=? and CISSP=? and TYPSP=? and KOD=?});
	$ucoExists->execute($osbId, 99, 'UCO', $UCO);

	if($ucoExists->fetch) {
		if($DEBUG == 1) { print "FIND: $osbId\n"; }
		# for now we never update existing entry
		if($DEBUG == 1) { print "SKIP RECORD: $osbId\n"; }
		$foundAndSkipped_SPJ++;

	} else {

		if($DEBUG == 1) { print "INSERT NEW SPJ_UCO RECORD: $osbId\n"; }
		$inserted_SPJ++;

		# we will do insert
		my $insertUco = $dbh->prepare(qq{INSERT INTO $tableName_SPJ (OSB_ID, UCO, CISSP, TYPSP, KOD, ZMENENO) VALUES (?,?,?,?,?,sysdate)});
		$insertUco->execute($osbId, $UCO, 99, 'UCO', $UCO);

	}

	# PROCESS SPJ TABLE - EMAIL (we can update EMAIL, we never remove)

	my $emailExist = $dbh->prepare(qq{select 1 from $tableName_SPJ where OSB_ID=? and CISSP=? and TYPSP=?});
	$emailExist->execute($osbId, 1, 'e-mail');

	if($emailExist->fetch) {
		if($DEBUG == 1) { print "FIND: $osbId\n"; }

		# we need to know if these two records are without changes, if yes, skip them
		my $recordAreEquals = $dbh->prepare(qq{SELECT 1 from $tableName_SPJ where OSB_ID=? and CISSP=? and TYPSP=? and KOD=?});
		$recordAreEquals->execute($osbId, 1, 'e-mail', $EMAIL);

		if(!$recordAreEquals->fetch) {

			my $updatePerson = $dbh->prepare(qq{UPDATE $tableName_SPJ SET KOD=?, ZMENENO=sysdate WHERE OSB_ID=? and UCO=? and CISSP=? and TYPSP=?});
			$updatePerson->execute($EMAIL, $osbId, $UCO, 1, 'e-mail');
			if($DEBUG == 1) { print "UPDATING EXISTING SPJ_EMAIL RECORD: $osbId\n"; }
			$foundAndUpdated_SPJ++;

		} else {

			if($DEBUG == 1) { print "SKIP SPJ_EMAIL RECORD: $osbId\n"; }
			$foundAndSkipped_SPJ++;

		}

	} else {

		if($DEBUG == 1) { print "INSERT NEW SPJ_EMAIL RECORD: $osbId\n"; }
		$inserted_SPJ++;

		# we will do insert
		my $insertEmail = $dbh->prepare(qq{INSERT INTO $tableName_SPJ (OSB_ID, UCO, CISSP, TYPSP, KOD, ZMENENO) VALUES (?,?,?,?,?,sysdate)});
		$insertEmail->execute($osbId, $UCO, 1, 'e-mail', $EMAIL);

	}

}

commit $dbh;
$dbh->disconnect();

#Info about operations
print "=======================================\n";
print "Newly inserted:   \t$inserted\n";
print "Found and skipped: \t$foundAndSkipped\n";
print "Found and updated:\t$foundAndUpdated\n";
print "=======================================\n";
print "Newly inserted SPJ:   \t$inserted_SPJ\n";
print "Found and skipped SPJ: \t$foundAndSkipped_SPJ\n";
print "Found and updated SPJ: \t$foundAndUpdated_SPJ\n";
print "=======================================\n";

$lock->unlock();
